varnishtest "Test saintmode vmod"

server s1 {
       rxreq
       expect req.url == "/a"
       txresp -hdr "Saint: yes" -hdr "Backend: s1"

       accept
       rxreq
       expect req.url == "/b"
       txresp -hdr "Backend: s1"

       accept
       rxreq
       expect req.url == "/a"
       txresp -hdr "Backend: s1"
} -start

server s2 {
       rxreq
       expect req.url == "/a"
       txresp -hdr "Backend: s2"
} -start

varnish v1 -vcl+backend {
	import saintmode from "${vmod_topbuild}/src/.libs/libvmod_saintmode.so";
	import directors from "${vmoddir}/libvmod_directors.so";

	sub vcl_init {
		new sm1 = saintmode.saintmode(s1, 5);
		new sm2 = saintmode.saintmode(s2, 5);
		new fb = directors.fallback();
		fb.add_backend(sm1.backend());
		fb.add_backend(sm2.backend());
	}

	sub vcl_recv {
		return (pass);
	}

	sub vcl_backend_fetch {
		set bereq.backend = fb.backend();
	}

	sub vcl_backend_response {
		if (beresp.http.Saint == "yes") {
			saintmode.blacklist(0.5s);
			return (retry);
		}
	}

} -start

client c1 {
	txreq -url "/a"
	rxresp
	expect resp.http.backend == "s2"
	expect resp.status == 200

	txreq -url "/b"
	rxresp
	expect resp.http.backend == "s1"
	expect resp.status == 200

	delay 0.5

	txreq -url "/a"
	rxresp
	expect resp.http.backend == "s1"
}

client c1 -run
